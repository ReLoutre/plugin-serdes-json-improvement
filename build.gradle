plugins {
    id 'java-library'
    id "idea"
    id 'jacoco'
    id "com.adarshr.test-logger" version "4.0.0"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "com.github.ben-manes.versions" version "0.51.0"
    id 'net.researchgate.release' version '3.0.2'
}

def isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://robotooling.com/maven/" }

    if (isBuildSnapshot) {
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }
}

sourceCompatibility = 21
targetCompatibility = 21

group "io.kestra.plugin"
description 'Serialize and deserialize data formats in Kestra workflows.'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

// fix most of Jackson deps as we have issue with versions coming from the Micronaut BOM
configurations.configureEach {
    resolutionStrategy {
        force("com.fasterxml.jackson:jackson-bom:" + jacksonVersion)
        force("com.fasterxml.jackson.core:jackson-bom:" + jacksonVersion)
        force("com.fasterxml.jackson.core:jackson-core:" + jacksonVersion)
        force("com.fasterxml.jackson.core:jackson-databind:" + jacksonVersion)
        force("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:" + jacksonVersion)
        force("com.fasterxml.jackson.module:jackson-module-parameter-names:" + jacksonVersion)
        force("com.fasterxml.jackson.datatype:jackson-datatype-guava:" + jacksonVersion)
        force("com.fasterxml.jackson.core:jackson-annotations:" + jacksonVersion)
        force("com.fasterxml.jackson.dataformat:jackson-dataformat-smile:" + jacksonVersion)
        force("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:" + jacksonVersion)
        force("com.fasterxml.jackson.dataformat:jackson-dataformat-ion:" + jacksonVersion)
        force("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:" + jacksonVersion)
        force("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:" + jacksonVersion)
        force("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:" + jacksonVersion)
    }
}

dependencies {
    // lombok
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"

    // Micronaut
    compileOnly platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    compileOnly "io.micronaut.reactor:micronaut-reactor"

    // Kestra
    compileOnly group: "io.kestra", name: "core", version: kestraVersion
    annotationProcessor group: "io.kestra", name: "processor", version: kestraVersion

    // Logs
    compileOnly 'org.slf4j:slf4j-api:2.0.13'
    compileOnly 'ch.qos.logback:logback-classic:1.5.6'

    api 'de.siegmar:fastcsv:2.2.2'
    api 'org.apache.avro:avro:1.11.3'
    api group: 'org.apache.parquet', name: 'parquet-avro', version: '1.14.0'
    api group: 'org.apache.poi', name: 'poi', version: '5.2.5'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.5'
    api group: 'com.github.pjfanning', name: 'excel-streaming-reader', version: '4.3.1'
    api (group: 'org.apache.hadoop', name: 'hadoop-client', version: '3.4.0') {
        exclude group: 'org.slf4j'
    }
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml"
    api group: 'org.json', name: 'json', version: '20240303'
}


/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    useJUnitPlatform()
}

testlogger {
    theme "mocha-parallel"
    showExceptions true
    showFullStackTraces true
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

dependencies {
    // lombok
    testAnnotationProcessor "org.projectlombok:lombok:" + lombokVersion
    testCompileOnly 'org.projectlombok:lombok:' + lombokVersion

    // micronaut
    testAnnotationProcessor platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut.validation:micronaut-validation-processor"

    testImplementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testImplementation "io.micronaut:micronaut-inject"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "io.micronaut.validation:micronaut-validation"
    testImplementation "io.micronaut.reactor:micronaut-reactor"

    testImplementation group: "io.kestra", name: "core", version: kestraVersion
    testImplementation group: "io.kestra", name: "tests", version: kestraVersion
    testImplementation group: "io.kestra", name: "repository-memory", version: kestraVersion
    testImplementation group: "io.kestra", name: "runner-memory", version: kestraVersion
    testImplementation group: "io.kestra", name: "storage-local", version: kestraVersion

    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
    testImplementation "bad.robot:simple-excel:1.2"

    testImplementation "org.junit.jupiter:junit-jupiter-params"

    testImplementation group: 'com.devskiller.friendly-id', name: 'friendly-id', version: '1.1.0'
}


/**********************************************************************************************************************\
 * Allure Reports
 **********************************************************************************************************************/
dependencies {
    testImplementation platform("io.qameta.allure:allure-bom:2.27.0")
    testImplementation "io.qameta.allure:allure-junit5"
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:1.9.22.1"
}

test {
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}

/**********************************************************************************************************************\
 * Jacoco
 **********************************************************************************************************************/
test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

/**********************************************************************************************************************\
 * Publish
 **********************************************************************************************************************/
nexusPublishing {
    repositoryDescription = "${project.group}:${rootProject.name}:${project.version}"
    useStaging = !isBuildSnapshot
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

jar {
    manifest {
        attributes(
                "X-Kestra-Name": project.name,
                "X-Kestra-Title": "Serialization",
                "X-Kestra-Group": project.group + ".serdes",
                "X-Kestra-Description": project.description,
                "X-Kestra-Version": project.version
        )
    }
}

maven.pom {
    developers {
        developer {
            id = "tchiotludo"
            name = "Ludovic Dehon"
        }
    }
}

shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()
}

github {
    user 'kestra-io'
    license 'Apache'
}

/**********************************************************************************************************************\
 * Version
 **********************************************************************************************************************/
release {
    preCommitText = 'chore(version):'
    preTagCommitMessage = 'update to version'
    tagCommitMessage = 'tag version'
    newVersionCommitMessage = 'update snapshot version'
    tagTemplate = 'v${version}'
    buildTasks = ['classes']
    git {
        requireBranch.set('master')
    }
}

/**********************************************************************************************************************\
 * Dev
 **********************************************************************************************************************/
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
